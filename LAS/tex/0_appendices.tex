\section{\gnuplot{} Quellcode zur Auswertung von Teilversuch 4}
    \label{appdx:gnuplottv4}
    {  
        % % Surpress "errors" in minted
        % https://tex.stackexchange.com/a/289068
        \renewcommand{\fcolorbox}[4][]{#4}
        \begin{minted}[linenos,breaklines,autogobble,frame=leftline,framesep=10pt]{gnuplot}
#!/usr/bin/env gnuplot

set term epslatex color size 6in, 4in
set output "tv4-plot.tex"
set decimalsign locale 'de_DE.UTF-8'

set title "Transmittierte Leistung gegen Strahldurchmesser"
set ylabel "Transmittierte Leistung $P$ ($\\si{\\milli\\watt}$)"
set xlabel "Durchmesser $d$ ($\\si{\\milli\\meter}$)"

set mxtics
set mytics
set samples 10000

f(x) = A*W*(1 - exp(-(x**2)/(2*W)))

# (x, y, xdelta, ydelta)
fit f(x) "tv4.dat" u 2:1:(0.1):(0.1) xyerrors via A,W

# Linien
set key bottom right spacing 2

titel = "$".gprintf("%.5f", A)."\\cdot".gprintf("%.5f", W)."\\left(1 - \\exp{-\\frac{d^2}{2(".gprintf("%.5f", W).")}}\\right)$"
plot f(x) title titel lc rgb 'dark-magenta', \
    "tv4.dat" u 2:1:(0.1):(0.1) with xyerrorbars title "Messpunkte" pointtype 0 lc rgb 'dark-goldenrod'
        \end{minted}
    }
    mit \texttt{tv4.dat}:
    \begin{multicols}{2}
        \begin{minted}[linenos,breaklines,autogobble,frame=leftline,framesep=10pt]{text}
# P/mW d/mm
8,0    14,0
7,9    9,0
7,8    7,5
7,7    7,1
7,6    7,0
7,5    6,9
7,2    6,5
7,0    6,1
6,5    5,3
6,2    5,0
5,7    4,2
5,1    3,8
4,5    3,5
3,9    3,0
2,0    2,1
1,0    1,0
0,7    0,7
        \end{minted}
    \end{multicols}
    \vspace{-\baselineskip}
    Rohausgabe:
    \begin{minted}[linenos,breaklines,autogobble,frame=leftline,framesep=10pt]{text}
final sum of squares of residuals : 47.4052
rel. change during last iteration : -5.53932e-06

degrees of freedom    (FIT_NDF)                        : 15
rms of residuals      (FIT_STDFIT) = sqrt(WSSR/ndf)    : 1.77774
variance of residuals (reduced chisquare) = WSSR/ndf   : 3.16035
p-value of the Chisq distribution (FIT_P)              : 3.17016e-05

Final set of parameters            Asymptotic Standard Error
=======================            ==========================
A               = 1.05673          +/- 0.0488       (4.618%)
W               = 7.45305          +/- 0.4013       (5.384%)

correlation matrix of the fit parameters:
                A      W      
A               1.000 
W              -0.985  1.000 
    \end{minted}